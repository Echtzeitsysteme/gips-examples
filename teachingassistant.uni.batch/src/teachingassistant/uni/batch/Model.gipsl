package "teachingassistant.uni.batch"
import "platform:/resource/teachingassistant.uni.metamodel/model/Metamodel.ecore"

config {
	solver := GUROBI;
	timeLimit := true [value := 120.0];
	randomSeed := true [value := 0];
	presolve := auto;
	debugOutput := true;
//	lpOutput := true [path := "problem.lp"];
}

//
// GT
//

// Find session occurrence
pattern findSessionOccurrence {
	session : TeachingSession {
		- occurrences -> occurrence
	}
	
	occurrence : SessionOccurrence
}

// Construct a condition for a session that a specific TA
// is available (i.e., they have not blocked this session)
pattern findTaUnavailableSession {
	ta : TeachingAssistant {
		- unavailableBecauseLessons -> entry
	}
	
	session : TeachingSession {
		- entries -> entry
	}
	
	entry : TimeTableEntry
}
condition taAvailableInSession = forbid findTaUnavailableSession

// Rule to assign a TA to a specific session occurrence.
// This rule respects the condition that the TA must not have
// blocked the respective time table entry of the session occurrence.
rule assignTa {
	ta : TeachingAssistant
	
	module : Module {
		- sessions -> session
		- approvals -> approval
	}
	
	session : TeachingSession {
		- occurrences -> occurrence
		- entries -> entry
	}
	
	occurrence : SessionOccurrence {
		++ - tas -> ta
	}
	
	w : Week
	
	entry : TimeTableEntry {
		- timeTableWeeks -> w
	}
	
	approval : EmploymentApproval {
		- ta -> ta
	}
	
	# occurrence.timeTableWeek == w.number
	# approval.ratingNumeric > 0
}
when taAvailableInSession

// Find every possible combination of TAs
// and time table entries
pattern findTaEntryCombination {
	ta : TeachingAssistant
	entry : TimeTableEntry
}

// For a specific TA, find all conflicting time table
// entries in the model.
// This pattern does respect partly overlapping time
// table entries.
pattern findConflictingEntriesWithTa {
	week : Week
	
	entryA : TimeTableEntry {
		- timeTableWeeks -> week
	}
	entryB : TimeTableEntry {
		- timeTableWeeks -> week
	}
	
	ta : TeachingAssistant
	
	# entryA.startEpoch < entryB.endEpoch
	# entryA.endEpoch > entryB.startEpoch
	
	// The day must match
	# entryA.weekDay == entryB.weekDay
}

// Find all possible combinations of TAs and weeks
pattern findTaWeekCombination {
	w : Week
	ta : TeachingAssistant
}

// Find a TA with a green employment approval status
// for a specific module.
pattern findGreenTa {
	ta : TeachingAssistant
	
	mod : Module {
		- approvals -> approval
	}
	
	approval : EmploymentApproval {
		- ta -> ta
	}
	# approval.ratingNumeric == 2
}

// Find a TA with an amber employment approval status
// for a specific module.
pattern findAmberTa {
	ta : TeachingAssistant
	
	mod : Module {
		- approvals -> approval
	}
	
	approval : EmploymentApproval {
		- ta -> ta
	}
	# approval.ratingNumeric == 1
}

pattern findInterCampusTimeTableEntriesConflict {
	ta : TeachingAssistant
	
	week : Week
	
	entryA : TimeTableEntry {
		- timeTableWeeks -> week
		- room -> roomA
	}
	roomA : Room {
		- campus -> campusA
	}
	campusA : Campus
	
	entryB : TimeTableEntry {
		- timeTableWeeks -> week
		- room -> roomB
	}
	roomB : Room {
		- campus -> campusB
	}
	campusB : Campus
	
	// Match day
	# entryA.weekDay == entryB.weekDay
	
	// Time frame between A.end and B.start is < 3600
	# entryB.startEpoch - entryA.endEpoch < 3600
	
	// No time conflict, A finished before B
	# entryA.endEpoch <= entryB.startEpoch
}

//
// GIPSL
//

// If a mapping instance has a value of `1`, the respective TA must
// be mapped to the respective session occurrence.
mapping taToOccurrence to assignTa;

// Every session occurrence must have exactly `numTAsPerSession` TAs assigned.
constraint with findSessionOccurrence {
	mappings.taToOccurrence->join((occurrence, occurrence))->sum(element.value)
	==
	context.nodes.session.numTasPerSession
}

// TAs must not have conflicting assignments.
// GIPSL formulation: For any pair of conflicting session occurrences (in the
// context of one TA), a maximum of one of these conflicting session can be chosen.
constraint with findConflictingEntriesWithTa {
	mappings.taToOccurrence->join((ta, ta), (entry, entryA))->sum(element.value)
	+ mappings.taToOccurrence->join((ta, ta), (entry, entryB))->sum(element.value)
	<= 1
}

// TAs must have at least 1h = 3600s to switch campus in between assignments.
// GIPSL formulation: For any pair of time table entries that could be assigned
// to a single TA, which require inter-campus travel, and which time window for
// a switch is less than 1h = 3600s, a maximum of one of these sessions can be
// chosen.
constraint with findInterCampusTimeTableEntriesConflict {
	mappings.taToOccurrence->join((ta, ta), (entry, entryA))->sum(element.value)
	+ mappings.taToOccurrence->join((ta, ta), (entry, entryB))->sum(element.value)
	<= 1
}

// Ensure TAs max hours per year
// GIPSL interpretation: the sum of all assigned working hours per TA
// must not exceed their yearly hour limit
constraint with TeachingAssistant {
	mappings.taToOccurrence->join(ta)->sum(element.value * element.nodes.session.hoursPaidPerOccurrence)
	<= context.maxHoursPerYear
}

// Ensure TAs max hours per week
// GIPSL interpretation: the sum of all assigned working hours per TA
// in a specific week must not exceed their weekly hour limit
constraint with findTaWeekCombination {
	mappings.taToOccurrence->join((ta, ta), (w, w))->sum(element.value * element.nodes.session.hoursPaidPerOccurrence)
	<= context.nodes.ta.maxHoursPerWeek
}

// Represents the number of assigned session occurrences to a specific
// TA for a specific session (and the TA needs to have the green
// employment rating for the respective module).
function greenTas with findGreenTa {
	mappings.taToOccurrence->join((ta, ta), (module, mod))->sum(element.value)
}

// Represents the number of assigned session occurrences to a specific
// TA for a specific session (and the TA needs to have the amber
// employment rating for the respective module).
function amberTas with findAmberTa {
	mappings.taToOccurrence->join((ta, ta), (module, mod))->sum(element.value)
}

// Global objective
// We want to use a higher weight for green TAs compared to the
// amber TAs
objective : max {
	0
	+ 2 * functions.greenTas
	+ 1 * functions.amberTas
}
