import "platform:/resource/ihtcvirtualmetamodel/model/Ihtcvirtualmetamodel.ecore"

//
// GT rules for the pre-processing of a (virtual) model
//
// Finds an existing virtual roster shift object for a specific
// roster and a specific shift.
pattern findVirtualRoster {
	root: Root {
		-nurses -> nurse
		-rooms -> room
		-virtualShiftToRoster -> v
	}

	nurse: Nurse {
		-rosters -> roster
	}

	roster: Roster

	room: Room {
		-shifts -> shift
	}

	shift: Shift

	v: VirtualShiftToRoster {
		-roster -> roster
		-shift -> shift
	}
	
	# roster.shiftNo == shift.shiftNo
}

condition noVirtualRoster = forbid findVirtualRoster

// Create a new virtual shift to roster object for a specific
// shift and a specific roster. There must not be an existing
// virtual shift to roster object connecting those two.
rule createVirtualShiftToRoster {
	root: Root {
		-nurses -> nurse
		-rooms -> room
		++ -virtualShiftToRoster -> v
	}

	nurse: Nurse {
		-rosters -> roster
	}

	roster: Roster

	room: Room {
		-shifts -> shift
	}

	shift: Shift

	++ v: VirtualShiftToRoster {
		++ -roster -> roster
		++ -shift -> shift
		.isSelected := false
		.wasImported := false
	}
	
	# roster.shiftNo == shift.shiftNo
}
when noVirtualRoster
