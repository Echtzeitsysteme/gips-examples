package "pta.gipsl.prototype"

import "platform:/resource/PersonTaskAssignments/model/PersonTaskAssignments.ecore"

config {
	solver := GUROBI [home:="c:\gurobi1000\win64", license:="c:\gurobi1000\win64\gurobi.lic"];
	launchConfig := true [main := "pta.example.house.HouseConstructionExample"];
	timeLimit := true [value := 600.0];
	randomSeed := true [value := 0];
	presolve := true;
	debugOutput := true;
	tolerance := true [value := 0.000001];
}


rule assignOffer {
	skill : Skill
	worker : Person {
		-offers->offer
		-skills->skill
	}
	week : Week
	offer : Offer {
		++ -requirements-> requirement
		- week -> week
	}
	
	requirement : Requirement
	
	task : Task {
		-requirements->requirement
		++ - weeks -> week
	}
	
	project : Project {
		- tasks -> task
		++ -persons -> worker
		++ -weeks -> week
	}
	
	# skill.name == requirement.name
	# week.number >= project.initialWeekNumber
	# week.number < project.initialWeekNumber + project.weeksUntilLoss
	# skill.level >= requirement.skillLevel
//	# worker.overtimeBonus <= requirement.overtimeBonus
//	# worker.salary <= requirement.salary
}

pattern taskSequence {
	week : Week
	task : Task {
		- next -> next
	}
	
	next : Task
	
	project : Project {
		- tasks -> task
	}
	
	# week.number >= project.initialWeekNumber
	# week.number < project.initialWeekNumber + project.weeksUntilLoss
}

pattern finalTaskSequence {
	week : Week
	finalTask : Task {
		- previous -> previous
	}
	
	previous : Task
	
	project : Project {
		- tasks -> finalTask
	}
	
	# week.number >= project.initialWeekNumber
	# week.number < project.initialWeekNumber + project.weeksUntilLoss
	
} when noNextTask

condition noNextTask = forbid taskHasNext

pattern taskHasNext {
	nextTask : Task
	finalTask : Task {
		-next->nextTask
	}
}

pattern projectInitialWeek {
	week : Week
	project : Project
	#project.initialWeekNumber == week.number
}


mapping aom with assignOffer;

// An person's offer might only be mapped once
constraint -> class::Offer {
	mappings.aom->filter(m | m.nodes().offer == self)->count() <= 1
}

// The sum of hours over all accepted offers must be at least as high as the requirement's hours, including overtime flexibility
constraint -> class::Requirement {
	mappings.aom->filter(m | m.nodes().requirement == self)->sum(m | m.nodes().offer.hours * m.nodes().worker.overtimeFlexibility) >= self.hours
}

constraint -> pattern::taskSequence {
	// A: Precondition -> The current week gets assigned to the current task
	// B: Then -> There must be no week with a smaller number assigned to the subsequent task
	// A => B : If week is a week of the current task assignment, then there must not exist a task assignment to a subsequent task with the identical week number or a lesser week number.
	// 1 => 1 = 1 : Week is part of the current task assignment & no smaller week number in subsequent task exists -> true
	// 1 => 0 = 0 : Week is part of the current task assignment & a smaller week number in subsequent task exists -> false
	// 0 => 1 = 1 : Week is not part of the current task assignment & a smaller week number in subsequent task exists -> true
	// 0 => 0 = 1 : Week is not part of the current task assignment & no smaller week number in subsequent task exists -> true
	mappings.aom->filter(m | m.nodes().task == self.nodes().task & m.nodes().week == self.nodes().week)->count() >= 1 =>
	mappings.aom->filter(m | m.nodes().task == self.nodes().next & m.nodes().week.number <= self.nodes().week.number)->count() <= 0
}

constraint -> pattern::finalTaskSequence {
	// A: Precondition -> The current week gets assigned to the current task
	// B: Then -> There must be no week with a smaller number assigned to the subsequent task
	// A => B : If week is a week of the current task assignment, then there must not exist a task assignment to a subsequent task with the identical week number or a lesser week number.
	// 1 => 1 = 1 : Week is part of the current task assignment & no smaller week number in subsequent task exists -> true
	// 1 => 0 = 0 : Week is part of the current task assignment & a smaller week number in subsequent task exists -> false
	// 0 => 1 = 1 : Week is not part of the current task assignment & a smaller week number in subsequent task exists -> true
	// 0 => 0 = 1 : Week is not part of the current task assignment & no smaller week number in subsequent task exists -> true
	mappings.aom->filter(m | m.nodes().task == self.nodes().finalTask & m.nodes().week == self.nodes().week)->count() >= 1 =>
	mappings.aom->filter(m | m.nodes().task == self.nodes().previous & m.nodes().week.number >= self.nodes().week.number)->count() <= 0
}

//// This constraint makes any loss-based solution infeasible
//constraint -> pattern::projectInitialWeek {
//	mappings.aom->filter(m | m.nodes().project == self.nodes().project & m.nodes().week.number >= self.nodes().week.number + self.nodes().project.weeksUntilLoss)->count() == 0
//}
//
//// All mapped weeks must an equal or higher number than the start week's number
//constraint -> pattern::projectInitialWeek {
//	mappings.aom->filter(m | m.nodes().project == self.nodes().project & m.nodes().week.number < self.nodes().project.initialWeekNumber)->count() == 0
//}

objective minWorkers -> class::Requirement {
	mappings.aom->filter(m | m.nodes().requirement == self)->sum(m | m.nodes().offer.hours * m.nodes().worker.salary)
}

objective minOvertime -> class::Requirement {
	mappings.aom->filter(m | m.nodes().requirement == self)->sum(m | m.nodes().offer.hours * m.nodes().worker.overtimeBonus) - self.hours * self.overtimeBonus
}

objective finishEarly -> class::Project {
	mappings.aom->filter(m | m.nodes().project == self)->sum(m | m.nodes().week.number)
}

global objective : min {
	minWorkers + minOvertime + finishEarly
}
